apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'


def keystorePropertiesFile = rootProject.file("app/keystore/keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def accessPropertiesFile = rootProject.file("properties/access.properties")
def accessProperties = new Properties()
accessProperties.load(new FileInputStream(accessPropertiesFile))

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["androidCompileSdkVersion"]
    buildToolsVersion globalConfiguration["androidBuildToolsVersion"]

    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }

    defaultConfig {
        applicationId "com.client.shop"
        minSdkVersion globalConfiguration["androidMinSdkVersion"]
        targetSdkVersion globalConfiguration["androidTargetSdkVersion"]
        testInstrumentationRunner globalConfiguration["testInstrumentationRunner"]

        versionCode 1
        versionName "1"

        multiDexEnabled true

        buildConfigField "String", "BASE_URL", accessProperties['baseUrl']
        buildConfigField "String", "BASE_DOMAIN", accessProperties['baseDomain']
        buildConfigField "String", "ACCESS_TOKEN", accessProperties['shopifyAccessToken']
        buildConfigField "String", "PRIVATE_AUTH_KEY", accessProperties['shopifyApiPrivateAuthKey']
        buildConfigField "String", "PRIVATE_AUTH_PASSWORD", accessProperties['shopifyApiPrivateAuthPassword']
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        qa {
            minifyEnabled false
            versionNameSuffix "-qa"
            signingConfig signingConfigs.config
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlinVersion"
    implementation "com.android.support:appcompat-v7:$androidSupportVersion"
    implementation "com.android.support:design:$androidSupportVersion"
    implementation "com.android.support:recyclerview-v7:$androidSupportVersion"
    implementation "com.android.support:cardview-v7:$androidSupportVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    api 'net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:2.1.0'
    implementation "com.facebook.fresco:fresco:$frescoVersion"
    implementation 'com.github.rubensousa:gravitysnaphelper:1.3'
    implementation 'me.relex:circleindicator:1.2.2@aar'

    implementation "com.hannesdorfmann.mosby3:mvp:$mosbyVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    implementation 'org.jsoup:jsoup:1.10.1'

    implementation "io.reactivex.rxjava2:rxjava:$rxVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"

    implementation('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true
    }

    implementation project(':data')
    implementation project(':domain')
    implementation project(':getaway')
    implementation project(':shopify')

    testImplementation "com.google.dagger:dagger:$daggerVersion"
    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"
    testImplementation "com.android.support:appcompat-v7:$androidSupportVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "com.nhaarman:mockito-kotlin-kt1.1:$mockitoKotlinVersion"
    androidTestImplementation "com.android.support.test:runner:$testRunnerVersion"
}
